import gleam/dict.{type Dict}
import gleam/option.{type Option, None, Some}
import gleam/result

import interpreter/runtime_error.{
  type RuntimeError, RuntimeError, UndefinedVariable,
}
import interpreter/types.{type Object}
import parse/token.{type Token}

pub type Environment {
  Environment(
    values: Dict(String, Object),
    // outer scope variables
    enclosing: Option(Environment),
  )
}

pub fn new() -> Environment {
  Environment(values: dict.new(), enclosing: None)
}

pub fn new_scope(env: Environment) -> Environment {
  Environment(values: dict.new(), enclosing: Some(env))
}

pub fn define(env: Environment, name: Token, value: Object) -> Environment {
  Environment(
    ..env,
    values: dict.insert(env.values, token.to_lexeme(name.type_), value),
  )
}

pub fn get(env: Environment, tok: Token) -> Result(Object, RuntimeError) {
  let Environment(values:, enclosing:) = env
  let got = dict.get(values, token.to_lexeme(tok.type_))
  case got, enclosing {
    Ok(obj), _ -> Ok(obj)
    Error(Nil), Some(outer) -> get(outer, tok)
    Error(Nil), None -> Error(RuntimeError(tok, UndefinedVariable))
  }
}

pub fn assign(
  env: Environment,
  name: Token,
  value: Object,
) -> Result(Environment, RuntimeError) {
  let Environment(values:, enclosing:) = env
  let has_key = dict.has_key(values, token.to_lexeme(name.type_))
  case has_key, enclosing {
    True, _ -> Ok(define(env, name, value))
    False, Some(outer) -> {
      use outer_env <- result.try(assign(outer, name, value))
      Ok(Environment(..env, enclosing: Some(outer_env)))
    }
    False, None -> Error(RuntimeError(name, UndefinedVariable))
  }
}
